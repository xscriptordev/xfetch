# Maintainer: xscriptor <x@xscriptor.com>
pkgname=xfetch-git
_pkgname=xfetch
pkgver=r17.b842749
pkgrel=1
pkgdesc="Custom system information tool for X (Rust)"
arch=('x86_64')
url="https://github.com/xscriptordev/xfetch"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'cargo')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

package() {
  cd "$_pkgname"
  
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$_pkgname/README.md"

  mkdir -p "$pkgdir/usr/share/$_pkgname"
  cp -r configs "$pkgdir/usr/share/$_pkgname/"
  cp -r logos "$pkgdir/usr/share/$_pkgname/"
}
